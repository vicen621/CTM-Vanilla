buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "java"
    id "idea"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "kr.entree.spigradle" version "2.4.3"
    id "io.freefair.lombok" version "6.6.1"
}

if (project.hasProperty("local_script")) {
    apply from: file(local_script + "/build.local.gradle")
}

sourceCompatibility = 17
targetCompatibility = 17

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

spigot {
    name project.property("pluginName")
    authors [project.property("author")]
    apiVersion project.property("apiVersion")
    softDepends 'PlaceholderAPI'
    permissions {
        'ctm.op' {
            defaults 'op'
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

archivesBaseName = project.property("pluginName")

repositories {
    mavenLocal()
    mavenCentral()
    paper()
    jitpack()
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url "https://s01.oss.sonatype.org/content/groups/public/" }
}

dependencies {
    // using using paper-api
    compileOnly paper(mcVersion)

    implementation 'com.github.Exlll.ConfigLib:configlib-paper:v4.2.0'
    implementation 'io.github.rysefoxx.inventory:RyseInventory-Plugin:1.5.7'
    compileOnly 'me.clip:placeholderapi:2.11.2'

    shadow "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    shadow "fr.mrmicky:fastboard:1.2.1"
}

shadowJar {
    archiveClassifier.set('')
    configurations = [project.configurations.runtimeClasspath, project.configurations.shadow]
    relocate 'co.aikar.commands', "${packageName}.libs.acf"
    relocate 'co.aikar.locales', "${packageName}.libs.locales"
    relocate "fr.mrmicky.fastboard", "${packageName}.libs.fastboard"
}

tasks.build.dependsOn(shadowJar)
tasks.prepareSpigotPlugins.dependsOn(shadowJar)

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'
